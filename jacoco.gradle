apply plugin: 'jacoco'

def jacocoExcludes = [
        '**/config',
        '**/BosApplication.class',
        '**/controller',

        // 로그인 검증 클래스. (@LoginCheck, @CurrentUserId)
        '**/CurrentUserIdResolver.class',
        '**/RequiredLoginException.class',
        '**/LoginCheckAspect.class',

        // 블랙리스트 체크 클래스.
        '**/BlackCheckAspect.class',
        '**/AccessDeniedException.class'
]

jacocoTestReport {
    reports {
        csv.enabled true

        afterEvaluate {
            getClassDirectories().setFrom(classDirectories.files.collect {
                fileTree(dir: it, exclude: jacocoExcludes)
            })
        }
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                minimum = 1.0
            }
        }
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                minimum = 1.0
            }
        }
    }
    afterEvaluate {
        getClassDirectories().setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        })
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification